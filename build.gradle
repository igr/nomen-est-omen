plugins {
    id 'java-library'
    id 'signing'
    id 'jacoco'
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}


group = 'com.oblac'
version = '2.2.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}
rootProject.description = 'Random names generator; for IDs or fun.'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "standardOut", "passed", "skipped", "failed"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

//
// MAVEN
//

ext.admin = hasProperty('sonatypeUsername')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'nomen-est-omen'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Nomen est Omen'
                description = 'Random names generator; for IDs or fun.'
                url = 'https://oblac.com"'
                licenses {
                    license {
                        name = 'BSD-2-Clause'
                        url = 'https://github.com/igr/nomen-est-omen/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'igor'
                        name = 'Igor SpasiÄ‡'
                        email = 'igor@jodd.org'
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/igr/nomen-est-omen.git'
                    connection = 'scm:git:git://github.com/igr/nomen-est-omen.git'
                    developerConnection = 'scm:git:ssh://git@github.com/igr/nomen-est-omen.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (admin) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

signing {
    required { admin }
    sign publishing.publications.mavenJava
}

task install(dependsOn: publishToMavenLocal) {
    group = 'Publishing'
    description = 'Installs artifacts to local Maven repository'
}

//
// RELEASE
//

task release() {
    group 'Project'
    description 'Builds everything for the release.'

    dependsOn clean
    dependsOn build
    dependsOn javadoc
    dependsOn jacocoTestReport
}

